// vim: ft=rust
use core::{RpNumber, Loc, RpPackage, ContentSlice};
use std::sync::Arc;
use ast::*;
use lexer::{self, Token};
use super::utils::strip_code_block;
use std::borrow::Cow;

grammar<S> where S: ContentSlice;

pub File: File<S> =
    <comment:"//!"?>
    <attributes:Loc<FileAttribute>*>
    <uses:Loc<Use>*>
    <decls:Decl*> => {
        File {
            comment: comment.unwrap_or_else(Vec::new),
            attributes: attributes,
            uses: uses,
            decls: decls,
        }
};

Use: UseDecl<S> =
    "use" <package:Loc<Package>>
        <range:Loc<"string">?>
        <alias:Loc<UseAlias>?>
    ";" => {
    UseDecl {
        package: package,
        range: range,
        alias: alias,
    }
};

UseAlias: S = "as" <value:ident> => value;

Package: RpPackage = <parts:OneOrMore<".", Ident>> => {
    RpPackage::new(parts.into_iter().map(|s| s.to_string()).collect())
};

Decl: Decl<S> = {
    <en:Item<Enum>> => Decl::Enum(en),
    <interface:Item<Interface>> => Decl::Interface(interface),
    <ty:Item<Type>> => Decl::Type(ty),
    <tuple:Item<Tuple>> => Decl::Tuple(tuple),
    <service:Item<Service>> => Decl::Service(service),
};

Enum: EnumBody<S> =
    "enum" <name:TypeIdent> "as" <ty:Loc<TypeSpec>> "{"
        <variants:Item<EnumVariant>*>
        <members:EnumMember*>
    "}" =>
    EnumBody {
        name: name,
        ty: ty,
        variants: variants,
        members: members,
    };

EnumMember: EnumMember<S> = {
    <code:Loc<Code>> => EnumMember::Code(code),
};

Interface: InterfaceBody<S> =
    "interface" <name:TypeIdent> "{"
      <members:TypeMember*>
      <sub_types:Item<SubType>*>
    "}" =>
    InterfaceBody {
        name: name,
        members: members,
        sub_types: sub_types,
    };

Type: TypeBody<S> =
    "type" <name:TypeIdent> "{"
        <members:TypeMember*>
    "}" =>
    TypeBody {
        name: name,
        members: members,
    };

Tuple: TupleBody<S> =
    "tuple" <name:TypeIdent> "{"
        <members:TypeMember*>
    "}" =>
    TupleBody {
        name: name,
        members: members,
    };

Service: ServiceBody<S> =
    "service" <name:TypeIdent> "{"
        <members:ServiceMember*>
    "}" =>
    ServiceBody {
        name: name,
        members: members,
    };

pub ServiceMember: ServiceMember<S> = {
    <endpoint:Item<Endpoint>> => {
        ServiceMember::Endpoint(endpoint)
    },

    <decl:Decl> => {
        ServiceMember::InnerDecl(decl)
    },
};

Endpoint: Endpoint<S> = {
    <id:Loc<Ident>> "("
        <arguments:ZeroOrMore<",", EndpointArgument>>
    ")" <response:("->" Loc<Channel>)?>
    <alias:FieldAlias?>
    ";" => {
        Endpoint {
            id: id,
            alias: alias,
            arguments: arguments,
            response: response.map(|r| r.1),
        }
    },
};

EndpointArgument: EndpointArgument<S> =
    <ident:Loc<Ident>> ":" <channel:Loc<Channel>> => EndpointArgument {
        ident: ident,
        channel: channel,
    };

Channel: Channel<S> = {
    <stream:stream?> <ty:TypeSpec> => {
        if stream.is_some() {
            Channel::Streaming {
                ty: ty
            }
        } else {
            Channel::Unary {
                ty: ty
            }
        }
    },
};

Ident: S = {
    any => "any".into(),
    float => "float".into(),
    double => "double".into(),
    u32 => "u32".into(),
    u64 => "u64".into(),
    i32 => "i32".into(),
    i64 => "i64".into(),
    boolean => "boolean".into(),
    string => "string".into(),
    datetime => "datetime".into(),
    bytes => "bytes".into(),
    stream => "stream".into(),
    "use" => "use".into(),
    "as" => "as".into(),
    "enum" => "enum".into(),
    "type" => "type".into(),
    "interface" => "interface".into(),
    "tuple" => "tuple".into(),
    "service" => "service".into(),
    <value:ident> => value,
};

FieldAlias: String = {
    "as" <value:Ident> => value.to_string(),
    "as" <value:"string"> => value,
};

EnumVariant: EnumVariant<S> =
    <name:Loc<TypeIdent>> <argument:("as" Loc<Value>)?> ";" =>
    EnumVariant {
        name: name,
        argument: argument.map(|a| a.1),
    };

Optional: bool = <modifier:"?"?> => modifier.map(|_| true).unwrap_or(false);

pub TypeMember: TypeMember<S> = {
    <field:Item<Field>> => TypeMember::Field(field),
    <code:Loc<Code>> => TypeMember::Code(code),
    <decl:Decl> => TypeMember::InnerDecl(decl),
};

Code: Code<S> =
    <attributes:Loc<Attribute>*>
    <context:Loc<Ident>> "{{" <content:code> "}}" =>
    Code {
        attributes: attributes,
        context: context,
        content: strip_code_block(content),
    };

Field: Field<S> = {
    <name:Ident> <optional:Optional> ":" <ty:Loc<TypeSpec>> <alias:FieldAlias?> ";" =>
    Field {
        required: !optional,
        name: name,
        ty: Loc::map(ty, ErrorRecovery::Value),
        field_as: alias,
    },

    <name:Ident> <optional:Optional> ":" <ty:Loc<!>> <alias:FieldAlias?> ";" =>
    Field {
        required: !optional,
        name: name,
        ty: Loc::map(ty, |e| ErrorRecovery::Error(e.dropped_tokens)),
        field_as: alias,
    },
};

SubType: SubType<S> = {
    <name:Loc<TypeIdent>> <alias:("as" Loc<Value>)?> "{"
        <members:TypeMember*>
    "}" =>
    SubType {
        name: name,
        members: members,
        alias: alias.map(|alias| alias.1),
    },

    <name:Loc<TypeIdent>> <alias:("as" Loc<Value>)?> ";" =>
    SubType {
        name: name,
        members: Vec::new(),
        alias: alias.map(|alias| alias.1),
    },
};

pub Value: Value<S> = {
    "(" <values:ZeroOrMore<",", Loc<Value>>> ")" => Value::Array(values),
    <string:"string"> => Value::String(string),
    <number:number> => Value::Number(number),
    <id:Ident> => Value::Identifier(id),
};

pub TypeSpec: Type<S> = {
    any => Type::Any,
    float => Type::Float,
    double => Type::Double,
    i32 => Type::Signed { size: 32 },
    i64 => Type::Signed { size: 64 },
    u32 => Type::Unsigned { size: 32 },
    u64 => Type::Unsigned { size: 64 },
    boolean => Type::Boolean,
    string => Type::String,
    datetime => Type::DateTime,
    bytes => Type::Bytes,
    <name:Name> => Type::Name{
        name: name
    },
    "[" <inner:TypeSpec> "]" => Type::Array {
        inner: Box::new(inner)
    },
    "{" <key:TypeSpec> ":" <value:TypeSpec> "}" => Type::Map{
        key: Box::new(key),
        value: Box::new(value)
    },
};

Name: Name<S> = {
    <prefix:(Loc<ident> "::")?> <parts:OneOrMore<"::", Loc<TypeIdent>>> => Name::Absolute {
        prefix: prefix.map(|p| p.0),
        parts: parts,
    },

    "::" <parts:OneOrMore<"::", Loc<TypeIdent>>> => Name::Relative {
        parts: parts,
    },
};

FileAttribute: Attribute<S> = {
    "#" "!" "[" <id:Loc<Ident>>  "]" => {
        Attribute::Word(id)
    },

    "#" "!" "[" <id:Loc<Ident>> "(" <name_value:ZeroOrMoreTrailing<",", AttributeItem>> ")" "]" => {
        Attribute::List(id, name_value)
    },
};

Attribute: Attribute<S> = {
    "#" "[" <id:Loc<Ident>>  "]" => {
        Attribute::Word(id)
    },

    "#" "[" <id:Loc<Ident>> "(" <name_value:ZeroOrMoreTrailing<",", AttributeItem>> ")" "]" => {
        Attribute::List(id, name_value)
    },
};

AttributeItem: AttributeItem<S> = {
    <id:Loc<Ident>> "=" <value:Loc<Value>> => AttributeItem::NameValue {
        name: id,
        value: value,
    },

    <value:Loc<Value>> => {
        AttributeItem::Word(value)
    },
};

/// Zero or more matching, separated by a token.
ZeroOrMore<Sep, T>: Vec<T> =
    <value:OneOrMore<Sep, T>?> =>
    value.unwrap_or_else(|| vec![]);

/// One or more matching, separated by a token.
OneOrMore<Sep, T>: Vec<T> =
     <values:(<T> Sep)*> <last:T> => {
        let mut values = values;
        values.push(last);
        values
    };

ZeroOrMoreTrailing<Sep, T>: Vec<T> = {
     <values:(<T> Sep)*> <last:T> Sep? => {
        let mut values = values;
        values.push(last);
        values
    },
    () => vec![],
};

Loc<Inner>: Loc<Inner> = <s:@L> <inner:Inner> <e:@R> =>
    Loc::new(inner, (s, e));

Item<T>: Item<S, T> =
    <comment:"///"?>
    <attributes:Loc<Attribute>*>
    <s:@L> <item:T> <e:@R> =>
    Item {
        comment: comment.unwrap_or_else(Vec::new),
        attributes: attributes,
        item: Loc::new(item, (s, e))
    };

extern {
    type Location = usize;
    type Error = lexer::Error;

    enum Token<S> {
        TypeIdent => Token::TypeIdentifier(<S>),
        ident => Token::Identifier(<S>),
        number => Token::Number(<RpNumber>),
        "use" => Token::Use,
        "as" => Token::As,
        "enum" => Token::Enum,
        "type" => Token::Type,
        "interface" => Token::Interface,
        "tuple" => Token::Tuple,
        "service" => Token::Service,
        "(" => Token::LeftParen,
        ")" => Token::RightParen,
        "{" => Token::LeftCurly,
        "}" => Token::RightCurly,
        "{{" => Token::CodeOpen,
        "}}" => Token::CodeClose,
        code => Token::CodeContent(<S>),
        "string" => Token::QuotedString(<String>),
        "[" => Token::LeftBracket,
        "]" => Token::RightBracket,
        ";" => Token::SemiColon,
        ":" => Token::Colon,
        "?" => Token::QuestionMark,
        "#" => Token::Hash,
        "!" => Token::Bang,
        "->" => Token::RightArrow,
        "," => Token::Comma,
        "." => Token::Dot,
        "::" => Token::Scope,
        "=" => Token::Equal,
        "//!" => Token::PackageDocComment(<Vec<S>>),
        "///" => Token::DocComment(<Vec<S>>),
        any => Token::Any,
        float => Token::Float,
        double => Token::Double,
        u32 => Token::U32,
        u64 => Token::U64,
        i32 => Token::I32,
        i64 => Token::I64,
        boolean => Token::Boolean,
        string => Token::String,
        datetime => Token::Datetime,
        bytes => Token::Bytes,
        stream => Token::Stream,
    }
}
